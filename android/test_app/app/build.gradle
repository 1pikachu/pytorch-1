apply plugin: 'com.android.application'

repositories {
    jcenter()
}

def props = new Properties()
file("../gradle.properties").withInputStream { props.load(it) }

def buildConfigProps = { k -> "\"${props.get(k)}\"" }

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "org.pytorch.testapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters ABI_FILTERS.split(",")
        }
        buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME'))
        buildConfigField ("String", "LOGCAT_TAG", "@string/app_name")
        addManifestPlaceholders([APP_NAME: "@string/app_name"])
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
    flavorDimensions "model"
    productFlavors {
        mbq{
            dimension "model"
            applicationIdSuffix ".mbq"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mobnet2q\"")
            buildConfigField ("int", "NUM_THREADS", "-1")
        }
        mbq1{
            dimension "model"
            applicationIdSuffix ".mbq1"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ1"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mobnet2q1\"")
            buildConfigField ("int", "NUM_THREADS", "1")
        }
        mbq2{
            dimension "model"
            applicationIdSuffix ".mbq2"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ2"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mobnet2q2\"")
            buildConfigField ("int", "NUM_THREADS", "2")
        }
        resnet18 {
            dimension "model"
            applicationIdSuffix ".rn"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_RESNET18'))
            addManifestPlaceholders([APP_NAME: "RN"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-resnet18\"")
            buildConfigField ("int", "NUM_THREADS", "-1")
        }
    }
    packagingOptions {
        pickFirst '**/libfbjni.so'
        doNotStrip '**.so'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation project(':pytorch_android')
    implementation project(':pytorch_android_torchvision')
}
